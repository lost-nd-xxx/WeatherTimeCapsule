//******************************************************************************
// YAYA　ゴーストテンプレート
// 種々のイベント辞書
//******************************************************************************

ScriptHead
{
	_color = ''
	_BalloonDir = SPLIT(SHIORI3FW.BalloonPath, '/', 0)
	_BalloonDir = _BalloonDir[ARRAYSIZE(_BalloonDir) - 2]
	if STRSTR(SHIORI3FW.BalloonName, 'WeatherTimeCapsule', 0) >= 0 || STRSTR(_BalloonDir, 'WeatherTimeCapsule', 0) >= 0 {
		_color = "\b[%(ColorTheme[0])]"
	}

	'\1\s[10]\0\s[0]'
	--
	_color
	--
	'\*\![enter,nouserbreakmode]\![set,balloonwait,0]\![set,balloontimeout,0]\![set,choicetimeout,0]\![close,inputbox,__SYSTEM_ALL_INPUT__]\_q'
}

ScriptHeadSimple
{
	_color = ''
	_BalloonDir = SPLIT(SHIORI3FW.BalloonPath, '/', 0)
	_BalloonDir = _BalloonDir[ARRAYSIZE(_BalloonDir) - 2]
	if STRSTR(SHIORI3FW.BalloonName, 'WeatherTimeCapsule', 0) >= 0 || STRSTR(_BalloonDir, 'WeatherTimeCapsule', 0) >= 0 {
		_color = "\b[%(ColorTheme[0])]"
	}

	'\1\s[10]\0\s[0]\_q' + _color
}

OnUserInputCancel
{
	_funcname = reference[0] + 'Cancel'
	if ISFUNC(_funcname) == 1 {
		EVAL(_funcname)
	}
}

//******************************************************************************
//シェル切り替え
//******************************************************************************

//------------------------------------------------------------------------------
//シェルチェンジ開始
//------------------------------------------------------------------------------
/*
OnShellChanging
{
'\1\s[10]\0\s[0]シェルを変更します\e'
}
*/

//------------------------------------------------------------------------------
//シェルチェンジ完了
//------------------------------------------------------------------------------
/*
OnShellChanged
{
_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])
"\1\s[10]\0\s[0]シェルを%(_escaped_name)へ変更しました\e"
}
*/

//******************************************************************************
//インストール関連
//******************************************************************************

//------------------------------------------------------------------------------
//インストール中のメッセージ
//------------------------------------------------------------------------------
OnInstallBegin
{
	ScriptHeadSimple
	--
	'インストールしています……\e'
}

//------------------------------------------------------------------------------
//インストール完了メッセージ
//------------------------------------------------------------------------------
OnInstallComplete
{
	_ref0 = SPLIT(reference[0], ' with', 2)[0] //hogehoge with hemohemo

	ScriptHeadSimple
	--
	if _ref0 == 'shell' {
		'シェル'
	}
	elseif _ref0 == 'ghost' {
		'ゴースト'
	}
	elseif _ref0 == 'balloon' {
		'バルーン'
	}
	elseif _ref0 == 'plugin' {
		'プラグイン'
	}
	--
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[1])
	"「%(_escaped_name)」、インストール完了しました。\n"
	--
	if basewarenameex == 'SSP' {
		_id = ''
		if _ref0 == 'shell' {
			_id = 'shell'
		}
		elseif _ref0 == 'ghost' {
			_id = 'ghost'
		}
		elseif _ref0 == 'balloon' {
			_id = 'balloon'
		}

		if _id != '' {
			'切り替えますか？'
			--
			AYATEMPLATE.MenuItem('切り替える', 'OnAYATEMPLATE.InstallChange', _id, 200)
			--
			AYATEMPLATE.MenuItem('切り替えない', 'script:\e')
		}
	}
}

OnAYATEMPLATE.InstallChange
{
	"\![change,%(AYATEMPLATE.EscapeText(reference[0])),lastinstalled]"
}

//------------------------------------------------------------------------------
//インストール失敗メッセージ
//------------------------------------------------------------------------------
OnInstallFailure
{
	ScriptHeadSimple
	--
	_escaped_message = SHIORI3FW.EscapeAllTags(reference[0])
	"インストール失敗。\n理由：%(_escaped_message)。\e"
}

//------------------------------------------------------------------------------
//インストール拒否（他のゴーストのためのアーカイブだった）
//------------------------------------------------------------------------------
OnInstallRefuse
{
	ScriptHeadSimple
	--
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])
	"インストール中断。\n「%(_escaped_name)」用のアーカイブです。\e"
}

//******************************************************************************
//消滅関連
//******************************************************************************

//------------------------------------------------------------------------------
//消滅指示
//------------------------------------------------------------------------------
//OnVanishSelecting

//------------------------------------------------------------------------------
//消滅開始
//------------------------------------------------------------------------------
//OnVanishSelected

//------------------------------------------------------------------------------
//消滅中止
//------------------------------------------------------------------------------
//OnVanishCancel

//------------------------------------------------------------------------------
//他のゴーストが消滅して自分に切り替わった
//------------------------------------------------------------------------------
//OnVanished

//******************************************************************************
//メールチェック
//******************************************************************************

//------------------------------------------------------------------------------
//メールチェック開始
//------------------------------------------------------------------------------
OnBIFFBegin
{
	ScriptHeadSimple
	--
	'メールチェック開始。'
	--
	'\e'
}

//------------------------------------------------------------------------------
//メールチェック成功
//------------------------------------------------------------------------------
OnBIFFComplete
{
	ScriptHeadSimple
	--
	if TOINT(reference[0]) == 0 {
		//---- メールは無かった
		'新着メールはありませんでした。'
	}
	else {
		//---- メールが届いていた
		"%(reference[0])通の新着メールがありました。"
	}
	--
	'\e'
}

//------------------------------------------------------------------------------
//メールチェック失敗
//------------------------------------------------------------------------------
OnBIFFFailure
{
	ScriptHeadSimple
	--
	_escaped_message = SHIORI3FW.EscapeAllTags(reference[0])
	"メールチェック失敗。\n理由：%(_escaped_message)。\e"
}

//******************************************************************************
//ネットワーク更新
//******************************************************************************

//------------------------------------------------------------------------------
//ネットワーク更新開始
//------------------------------------------------------------------------------
OnUpdateBegin
{
	ScriptHeadSimple
	--
	'ネットワーク更新があるか調べます。'
	--
	'\e'
}

//------------------------------------------------------------------------------
//ネットワーク更新準備完了
//------------------------------------------------------------------------------
OnUpdateReady
{
	ScriptHeadSimple
	--
	'ネットワーク更新を検知しました。\n更新開始します。'
	--
	'\e'
}

//------------------------------------------------------------------------------
//ネットワーク更新終了
//------------------------------------------------------------------------------
OnUpdateComplete
{
	ScriptHeadSimple
	--
	if reference[0] == 'none' {
		//---- 更新すべきファイルが無かった
		'更新はありませんでした。'
	}
	else {
		//---- 実際に更新が完了した
		'更新完了しました。'
	}
	--
	'\e'
}

//------------------------------------------------------------------------------
//ネットワーク更新失敗
//------------------------------------------------------------------------------
OnUpdateFailure
{
	ScriptHeadSimple
	--
	_escaped_message = SHIORI3FW.EscapeAllTags(reference[0])
	"ネットワーク更新失敗。\n理由：%(_escaped_message)。\e"
}

//******************************************************************************
//ヘッドラインセンス
//******************************************************************************

//------------------------------------------------------------------------------
//ヘッドラインセンス開始
//------------------------------------------------------------------------------
OnHeadlinesenseBegin
{
	ScriptHeadSimple
	--
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])
	"%(_escaped_name)ヘッドライン取得中。\e"
}

//------------------------------------------------------------------------------
//ヘッドライン読み上げ
//------------------------------------------------------------------------------
OnHeadlinesense.OnFind
{
	ScriptHeadSimple
	--
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])
	//---- はじめのセリフ
	if reference[2] == 'First' || reference[2] == 'First and Last' {
		//---- 最初のページ
		"%(_escaped_name)のヘッドラインです。"
	}
	elseif reference[2] == 'Next' {
		//---- 真ん中あたりのページ
		"%(_escaped_name)のヘッドラインの続きです。"
	}
	elseif reference[2] == 'Last' {
		//---- 最後のページ
		"%(_escaped_name)のヘッドライン、これが最後のページです。"
	}
	--
	//---- ヘッドライン本体
	"\n\n%(reference[3])\n"
	--
	//---- 選択肢
	if reference[2] == 'First' || reference[2] == 'Next' {
		//---- 次のページがある場合
		AYATEMPLATE.MenuItem('次のページ', '')
		--
		AYATEMPLATE.MenuItem('サイトへ', reference[1])
		--
		AYATEMPLATE.MenuItem('終了', 'Menu_HEADLINECANCEL', 150)
	}
	else {
		//---- 次のページがない場合
		AYATEMPLATE.MenuItem('サイトへ', reference[1])
		--
		AYATEMPLATE.MenuItem('終了', 'Menu_HEADLINECANCEL', 150)
	}
	--
	'\e'
}

//------------------------------------------------------------------------------
//ヘッドライン更新が無かった
//------------------------------------------------------------------------------
OnHeadlinesenseComplete
{
	ScriptHeadSimple
	--
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])
	"%(_escaped_name)、更新はないみたいです。\e"
}

//------------------------------------------------------------------------------
//ヘッドラインセンス失敗
//------------------------------------------------------------------------------
OnHeadlinesenseFailure
{
	ScriptHeadSimple
	--
	_escaped_message = SHIORI3FW.EscapeAllTags(reference[0])
	"ヘッドラインセンス失敗。\n理由：%(_escaped_message)。\e"
}

//******************************************************************************
//ファイル作成関連
//******************************************************************************

//------------------------------------------------------------------------------
//updates2.dau作成開始
//------------------------------------------------------------------------------
OnUpdatedataCreating
{
	ScriptHeadSimple
	--
	'アップデートデータを作成しています。'
	--
	'\e'
}

//------------------------------------------------------------------------------
//updates2.dau作成完了
//------------------------------------------------------------------------------
OnUpdatedataCreated
{
	ScriptHeadSimple
	--
	'アップデートデータ作成完了。'
	--
	'\e'
}

//------------------------------------------------------------------------------
//NAR作成開始
//------------------------------------------------------------------------------
OnNarCreating
{
	ScriptHeadSimple
	--
	'NAR作成開始。'
	--
	'\e'
}

//------------------------------------------------------------------------------
//NAR作成完了
//------------------------------------------------------------------------------
OnNarCreated
{
	ScriptHeadSimple
	--
	"「%(SHIORI3FW.EscapeAllTags(reference[0]))」のnarファイル、\n「%(SHIORI3FW.EscapeAllTags(reference[1]))」が完成しました。"
	--
	'\e'
}

//******************************************************************************
//時計合わせ
//みか (http://homepage2.nifty.com/ko-ki/) の辞書を参考にしました。感謝＞こーき氏
//******************************************************************************

//------------------------------------------------------------------------------
//時計合わせ開始
//------------------------------------------------------------------------------
OnSNTPBegin
{
	ScriptHeadSimple
	--
	"時計合わせ確認開始。\n%(SHIORI3FW.EscapeAllTags(reference[0]))に接続します。"
	--
	'\e'
}

//------------------------------------------------------------------------------
//時刻取得成功、比較開始
//この関数で 2008/03/25 19:15:05 のように見やすい形に作り変えます。
//詳しくはSTRFORM関数の説明を見てね！
//わからなければこのままで。
//------------------------------------------------------------------------------

SNTPCompare_StrForm
{
	_txt = SPLIT(_argv[0], ',')
	STRFORM('$04d/$02d/$02d $02d:$02d:$02d', TOINT(_txt[0]), TOINT(_txt[1]), TOINT(_txt[2]), TOINT(_txt[3]), TOINT(_txt[4]), TOINT(_txt[5]))
}

OnSNTPCompare
{
	ScriptHeadSimple
	--
	"\_q/
	修正前 %(SNTPCompare_StrForm(reference[2]))\n/
	修正後 %(SNTPCompare_StrForm(reference[1]))\n/
	\_q\n"
	--
	if reference[3] == 0 {
		'修正の必要はありませんでした。\e'
	}
	else {
		//\6は補正実行タグ。消しちゃだめ
		'\6'
		--
		"%(SHIORI3FW.EscapeAllTags(reference[3]))秒のずれを修正しました。"
	}
	--
	'\e'
}

//------------------------------------------------------------------------------
//時計合わせ失敗
//------------------------------------------------------------------------------
OnSNTPFailure
{
	ScriptHeadSimple
	--
	'時計合わせ失敗。'
	--
	'\e'
}

//******************************************************************************
//そのほか
//******************************************************************************

//------------------------------------------------------------------------------
//ディスプレイ解像度変更
//------------------------------------------------------------------------------
OnDisplayChange
{
	//---- 変数にディスプレイ解像度と色深度に関する情報を取得
	//---- 使うかどうかは分からないがとりあえず保持しておく
	displaybpp = reference[0]
	displaywidth = reference[1]
	displayheight = reference[2]
	SHIORI3FW.RegisterTempVar('displaybpp', 'displaywidth', 'displayheight')
}

//------------------------------------------------------------------------------
//URLドロップ
//CROW でゴーストインストーラからゴーストをインストールを始めた時にも呼ばれます
//------------------------------------------------------------------------------
OnURLDropping
{
	ScriptHeadSimple
	--
	"%(SHIORI3FW.EscapeAllTags(reference[0]))からデータを取得しています。"
	--
	'\e'
}


